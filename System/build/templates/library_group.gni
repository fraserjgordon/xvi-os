import("//build/templates/library.gni")

template("library_group")
{
    static_deps = []
    shared_deps = []
    kernel_deps = []

    default_build_targets = []
    test_targets = []

    if (defined(invoker.deps))
    {
        foreach(dep, invoker.deps)
        {
            dep_label = get_label_info(dep, "label_no_toolchain")
            dep_toolchain = get_label_info(dep, "toolchain")

            static_deps +=
            [
                "${dep_label}__static(${dep_toolchain})",
            ]

            shared_deps +=
            [
                "${dep_label}__shared(${dep_toolchain})",
            ]

            kernel_deps +=
            [
                "${dep_label}__kernel(${dep_toolchain})",
            ]

            default_build_targets +=
            [
                "${dep_label}__default_build_targets(${dep_toolchain})",
            ]

            test_targets +=
            [
                "${dep_label}__test_targets(${dep_toolchain})",
            ]
        }
    }

    if (defined(invoker.plain_deps))
    {
        static_deps += invoker.plain_deps
        shared_deps += invoker.plain_deps
        kernel_deps += invoker.plain_deps
    }

    group("${target_name}__static")
    {
        deps = static_deps
    }

    group("${target_name}__shared")
    {
        deps = shared_deps
    }

    group("${target_name}__kernel")
    {
        deps = kernel_deps
    }

    group("${target_name}__default_build_targets")
    {
        deps = default_build_targets
    }

    group("${target_name}__test_targets")
    {
        deps = test_targets
        testonly = true
    }
}
