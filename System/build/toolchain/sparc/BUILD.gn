import("//build/config/global_args.gni")
import("//build/toolchain/gcc_toolchain.gni")

declare_args()
{
    # Prefix used for compiler executables (e.g. "sparc-xvi-"")
    sparc_toolchain_prefix = ""
}

if (cross_toolchain_root != "" && sparc_toolchain_prefix == "")
{
    sparc_toolchain_prefix = "sparc-xvi-"
}

# Common settings for all Sparc GCC toolchains.
vars_gcc =
{
    arch_cflags     = []
    toolchain_arch  = "sparc"
    toolchain_os    = "xvi"

    if (cross_toolchain_root != "")
    {
        toolchain_root = cross_toolchain_root
    }

    if (sparc_toolchain_prefix != "")
    {
        toolchain_prefix = sparc_toolchain_prefix
    }
}

# Settings for v8 Sparc GCC toolchains.
vars_gcc_sparcv8 =
{
    forward_variables_from(vars_gcc, "*")

    arch_cflags += [ "-mcpu=v8" ]
    toolchain_cpu = "sparcv8"
}

# Settings for v9 Sparc GCC toolchains.
vars_gcc_sparcv9 =
{
    forward_variables_from(vars_gcc, "*")

    arch_cflags += [ "-mcpu=v9", "-m64" ]
    toolchain_cpu = "sparcv9"
}

if (use_clang)
{

}
else
{
    gcc_toolchain("sparcv8")
    {
        forward_variables_from(vars_gcc_sparcv8, "*")
    }

    gcc_toolchain("sparcv9")
    {
        forward_variables_from(vars_gcc_sparcv9, "*")
    }
}
