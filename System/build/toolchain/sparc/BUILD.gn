import("//build/config/global_args.gni")
import("//build/toolchain/gcc_toolchain.gni")

declare_args()
{
    # Prefix used for compiler executables (e.g. "sparc-xvi-"")
    sparc_toolchain_prefix = ""
    sparc64_toolchain_prefix = ""
}

if (cross_toolchain_root != "" && sparc_toolchain_prefix == "")
{
    sparc_toolchain_prefix = "sparc-xvi-"
}

if (cross_toolchain_root != "" && sparc64_toolchain_prefix == "")
{
    sparc64_toolchain_prefix = "sparc64-xvi-"
}

if (sparc64_toolchain_prefix == "" && sparc_toolchain_prefix != "")
{
    sparc64_toolchain_prefix = sparc_toolchain_prefix
}

# Common settings for all Sparc GCC toolchains.
vars_gcc_common =
{
    arch_cflags     = []
    toolchain_arch  = "sparc"
    toolchain_os    = "xvi"

    if (cross_toolchain_root != "")
    {
        toolchain_root = cross_toolchain_root
    }
}

vars_gcc_32 =
{
    forward_variables_from(vars_gcc_common, "*")

    if (sparc_toolchain_prefix != "")
    {
        toolchain_prefix = sparc_toolchain_prefix
    }
}

vars_gcc_64 =
{
    forward_variables_from(vars_gcc_common, "*")

    if (sparc64_toolchain_prefix != "")
    {
        toolchain_prefix = sparc64_toolchain_prefix
    }
}

# Settings for v8 Sparc GCC toolchains.
vars_gcc_sparcv8 =
{
    forward_variables_from(vars_gcc_32, "*")

    clang_target = "sparc"

    #arch_cflags += [ "-mcpu=v8" ]
    toolchain_cpu = "sparcv8"
}

# Settings for v9 Sparc GCC toolchains.
vars_gcc_sparcv9 =
{
    forward_variables_from(vars_gcc_64, "*")

    clang_target = "sparcv9"

    #arch_cflags += [ "-mcpu=v9", "-m64" ]
    toolchain_cpu = "sparcv9"
}

gcc_toolchain("v8")
{
    forward_variables_from(vars_gcc_sparcv8, "*")
}

gcc_toolchain("v9")
{
    forward_variables_from(vars_gcc_sparcv9, "*")
}
