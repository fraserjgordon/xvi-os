import("//build/config/global_args.gni")
import("//build/toolchain/gcc_toolchain.gni")

declare_args()
{
    # Prefix used for compiler executables (e.g. "arm-xvi-"")
    arm_toolchain_prefix = ""
    arm64_toolchain_prefix = ""
}

if (cross_toolchain_root != "" && arm_toolchain_prefix == "")
{
    arm_toolchain_prefix = "arm-xvi-"
}

if (cross_toolchain_root != "" && arm64_toolchain_prefix == "")
{
    arm64_toolchain_prefix = "aarch64-xvi-"
}

# Common settings for all ARM GCC toolchains.
vars_gcc_common =
{
    arch_cflags     = []
    toolchain_arch  = "arm"
    toolchain_os    = "xvi"

    if (cross_toolchain_root != "")
    {
        toolchain_root = cross_toolchain_root
    }
    
    arch_toolchain_args =
    {
        arm_arch_major = 0
        arm_arch_minor = 0
        arm_arch_profile = "a"
    }
}

vars_gcc_arm32 =
{
    forward_variables_from(vars_gcc_common, "*")

    if (arm_toolchain_prefix != "")
    {
        toolchain_prefix = arm_toolchain_prefix
    }
}

vars_gcc_arm64 =
{
    forward_variables_from(vars_gcc_common, "*")

    if (arm64_toolchain_prefix != "")
    {
        toolchain_prefix = arm64_toolchain_prefix
    }
}

# Settings for v6 ARM GCC toolchains.
vars_gcc_armv6 =
{
    forward_variables_from(vars_gcc_arm32, "*")

    arch_cflags += [ "-march=armv6" ]
    toolchain_cpu = "armv6"

    if (use_clang)
    {
        arch_cflags += [ "-mfloat-abi=soft" ]
    }

    _arch_toolchain_args =
    {
        forward_variables_from(arch_toolchain_args, "*")

        arm_arch_major = 6
    }
    arch_toolchain_args = {}
    arch_toolchain_args = _arch_toolchain_args
}

# Settings for v7 ARM GCC toolchains.
vars_gcc_armv7a =
{
    forward_variables_from(vars_gcc_arm32, "*")

    arch_cflags += [ "-march=armv7-a" ]
    toolchain_cpu = "armv7-a"

    _arch_toolchain_args =
    {
        forward_variables_from(arch_toolchain_args, "*")

        arm_arch_major = 7
    }
    arch_toolchain_args = {}
    arch_toolchain_args = _arch_toolchain_args
}

vars_gcc_armv8a =
{
    forward_variables_from(vars_gcc_arm64, "*")

    arch_cflags += [ "-march=armv8-a" ]
    toolchain_cpu = "armv8-a"

    _arch_toolchain_args =
    {
        forward_variables_from(arch_toolchain_args, "*")

        arm_arch_major = 8
        arm_arch_minor = 0
    }
    arch_toolchain_args = {}
    arch_toolchain_args = _arch_toolchain_args
}

vars_gcc_armv8_1a =
{
    forward_variables_from(vars_gcc_arm64, "*")

    arch_cflags += [ "-march=armv8.1-a" ]
    toolchain_cpu = "armv8.1-a"

    _arch_toolchain_args =
    {
        forward_variables_from(arch_toolchain_args, "*")

        arm_arch_major = 8
        arm_arch_minor = 1
    }
    arch_toolchain_args = {}
    arch_toolchain_args = _arch_toolchain_args
}

gcc_toolchain("armv6")
{
    forward_variables_from(vars_gcc_armv6, "*")
}

gcc_toolchain("armv7-a")
{
    forward_variables_from(vars_gcc_armv7a, "*")
}

gcc_toolchain("armv8-a")
{
    forward_variables_from(vars_gcc_armv8a, "*")
}

gcc_toolchain("armv8.1-a")
{
    forward_variables_from(vars_gcc_armv8_1a, "*")
}
