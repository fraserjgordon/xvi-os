# Default configurations to apply to all builds.
default_configs =
[
    "//build/config:c11",
    "//build/config:c++23",
    "//build/config:default_codegen",
    "//build/config:default_debug_info",
    "//build/config:default_errors",
    "//build/config:default_warnings",
    "//build/config:hidden_visibility",
    "//build/config:clang_sized_deallocation",
    "//build/config:gcc_colour_output",
    "//build/config:gcc_gc_crash_fix",
    "//build/config:rust_2021",
]

if (debug && !host_build)
{
    default_configs +=
    [
        "//build/config:optimize_debug"
    ]
}
else
{
    default_configs +=
    [
        "//build/config:optimize_full"
    ]
}

if (werror)
{
    default_configs +=
    [
        "//build/config:werror",
    ]
}

if (host_build)
{
    default_configs +=
    [
        "//build/config:hosted"
    ]
}

foreach(ts, test_sanitizers)
{
    if (ts == "address")
    {
        default_configs += ["//build/config:sanitize_address"]
    }
    else if (ts == "memory")
    {
        default_configs += ["//build/config:sanitize_memory"]
    }
    else if (ts == "leak")
    {
        default_configs += ["//build/config:sanitize_leak"]
    }
    else if (ts == "undefined")
    {
        default_configs += ["//build/config:sanitize_undefined"]
    }
    else
    {
        assert(false, "unknown sanitizer \"{ts}\"")
    }
}
