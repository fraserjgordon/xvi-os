OUTPUT_FORMAT(elf32-i386)
ENTRY(RealModeEntry)

SECTIONS
{
	# The real-mode entry is written to start at offset zero within a segment.
	rmentry 0x0000 : { *(rmentry) }

	# Other real-mode code follows the entry code.
	realmode : { *(realmode) }

	# Everything else is set up to run at its expected linear load address.
	.text (0x10000 + SIZEOF(rmentry) + SIZEOF(realmode)) : AT(ADDR(realmode) + SIZEOF(realmode))
	{ 
		*(.text)
		*(.text.*)
	}
	.rodata : { *(.rodata*) }
	.data : { *(.data*) }

	# Stage1 loader parameter area.
	params : { *(params) }

	# Comes after the parameter area as it takes up no size in the image.
	.bss : { *(.bss*) }

	BSS_Start = ADDR(.bss);
	BSS_Size = SIZEOF(.bss);

	# Discarded by objcopy.
	discardme :
	{
		*(.tbss*)
		*(.tdata*)
		*(.gcc_except_table*)
		*(.eh_frame*)
	}

	/DISCARD/ :
	{ 
		#*(*)
		*(.note*)
	}
}

