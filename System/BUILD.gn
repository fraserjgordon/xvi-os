subfolders =
[
    "ABI",
    "Allocator",
    "Boot",
    "C",
    "C++",
    "Compress",
    "Concurrency",
    "Executable",
    "Filesystem",
    "Firmware",
    "HW",
    "Rust",
    "Storage",
    "Unicode",
    "Utility",
]

group("default")
{
    deps = []
    foreach(subfolder, subfolders)
    {
        deps += ["${subfolder}/:${subfolder}__default_build_targets"]
    }

    if (build_all)
    {
        deps +=
        [
            ":all__static",
            ":all__shared",
            ":all__kernel",
        ]
    }
}

group("host_tools")
{
    deps =
    [
        "//Boot/Igniter/:IgniterTool(${host_toolchain})",
    ]
}

group("test_single_arch")
{
    testonly = true
    deps = []
    foreach(subfolder, subfolders)
    {
        deps += ["${subfolder}/:${subfolder}__test_targets"]
    }

    if (build_all || test_all)
    {
        deps +=
        [
            ":all__test_targets",
        ]
    }
}

group("test")
{
    testonly = true
    deps = [":test_single_arch(${host_toolchain})"]
}

generated_file("test_metadata")
{
    testonly = true
    outputs = [ "${root_build_dir}/test_exes.list" ]
    data_keys = [ "test_exes" ]

    deps = [ ":test" ]
}

library_group("all_single_arch")
{
    deps = subfolders   
}

library_group("all")
{
    if (enable_archs == [])
    {
        deps = [":all_single_arch"]
    }
    else
    {
        deps = []
        foreach(arch, enable_archs)
        {
            _arch = {}
            _arch =
            {
                import("//build/config/arch/arch_toolchains.gni")

                foreach(t, arch_toolchains_list)
                {
                    tt = []
                    tt = string_split(t, "!")
                    if (tt[0] == arch)
                    {
                        arch_toolchain = "//build/toolchain/${tt[1]}"
                    }
                }

                assert(defined(arch_toolchain), "unrecognised arch: ${arch}")
            }

            deps += [":all_single_arch(${_arch.arch_toolchain})"]
        }
    }
}
